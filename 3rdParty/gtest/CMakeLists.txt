#parent must define VERSION_GTEST

cmake_minimum_required(VERSION 3.0.0 FATAL_ERROR)
PROJECT(gtest)
STRING(TOUPPER ${PROJECT_NAME} PROJECT_NAME_UPPER)

SET(VERSION_${PROJECT_NAME_UPPER} "?.?.?" CACHE STRING "${PROJECT_NAME} version string")
SET(${PROJECT_NAME_UPPER}_DIR ${CMAKE_CURRENT_BINARY_DIR}/src CACHE STRING "${PROJECT_NAME} source directory")

include(ExternalProject)
ExternalProject_Add(
	${PROJECT_NAME}-ext
	URL ${CMAKE_CURRENT_SOURCE_DIR}/${PROJECT_NAME}-${VERSION_${PROJECT_NAME_UPPER}}.zip
	DOWNLOAD_DIR ${${PROJECT_NAME_UPPER}_DIR}
	SOURCE_DIR ${${PROJECT_NAME_UPPER}_DIR}
	CMAKE_ARGS -DBUILD_GMOCK=OFF -DBUILD_GTEST=ON -Dgtest_force_shared_crt=ON
	INSTALL_COMMAND ""
	BUILD_IN_SOURCE 1
)

# ------------------------------------------------------------------------------------------------------------------------------
# EXPORT VARIABLES: Variables intended to help other projects find and use this library
# ------------------------------------------------------------------------------------------------------------------------------

# Library
add_library(${PROJECT_NAME} STATIC IMPORTED GLOBAL)
IF(WIN32)
	SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES 
		IMPORTED_LOCATION_DEBUG ${${PROJECT_NAME_UPPER}_DIR}/googletest/Debug/${CMAKE_FIND_LIBRARY_PREFIXES}${PROJECT_NAME}${CMAKE_FIND_LIBRARY_SUFFIXES}
		IMPORTED_LOCATION_RELEASE ${${PROJECT_NAME_UPPER}_DIR}/googletest/Release/${CMAKE_FIND_LIBRARY_PREFIXES}${PROJECT_NAME}${CMAKE_FIND_LIBRARY_SUFFIXES}
		IMPORTED_LOCATION_MINSIZEREL ${${PROJECT_NAME_UPPER}_DIR}/googletest/MinSizeRel/${CMAKE_FIND_LIBRARY_PREFIXES}${PROJECT_NAME}${CMAKE_FIND_LIBRARY_SUFFIXES}
		IMPORTED_LOCATION_RELWITHDEBINFO ${${PROJECT_NAME_UPPER}_DIR}/googletest/RelWithDebInfo/${CMAKE_FIND_LIBRARY_PREFIXES}${PROJECT_NAME}${CMAKE_FIND_LIBRARY_SUFFIXES}
	)
ELSE(WIN32)
	SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES 
		IMPORTED_LOCATION ${${PROJECT_NAME_UPPER}_DIR}/${CMAKE_FIND_LIBRARY_PREFIXES}${PROJECT_NAME}${CMAKE_STATIC_LIBRARY_SUFFIX}
		INTERFACE_LINK_LIBRARIES -pthread
	)
ENDIF(WIN32)
add_dependencies(${PROJECT_NAME} ${PROJECT_NAME}-ext)

SET(INCLUDE_DIR ${${PROJECT_NAME_UPPER}_DIR}/googletest/include)
FILE(MAKE_DIRECTORY ${INCLUDE_DIR})
SET_TARGET_PROPERTIES(${PROJECT_NAME} PROPERTIES INTERFACE_INCLUDE_DIRECTORIES ${INCLUDE_DIR})

MARK_AS_ADVANCED(${PROJECT_NAME_UPPER}_DIR)